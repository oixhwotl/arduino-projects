/* BOARD and PIN SETTINGS BEGIN */
#define USE_ARDUINO   0
#define USE_ESP       1

#if USE_ARDUINO

#define PIN_DHT       3     // Digital pin connected to the DHT sensor
#define DHT_TYPE      DHT11   // DHT 11

#define PIN_CDS       12    // Photoresistor

#define PIN_LED_ENA   5   // Mortor Driver ENA
#define PIN_LED_IN1   6   // Mortor Driver IN1
#define PIN_LED_IN2   7   // Mortor Driver IN2

#define PIN_SOIL      A0

#define PIN_MOTOR_ENB 10
#define PIN_MOTOR_IN3 9
#define PIN_MOTOR_IN4 8

#else // USE_ESP32

#define analogWrite   dacWrite

#define LED_BUILTIN   2

#define PIN_DHT       4
#define DHT_TYPE      DHT11   // DHT 11

#define PIN_CDS       35    // Photoresistor

#define PIN_LED_ENA   26   // Mortor Driver ENA
#define PIN_LED_IN1   27   // Mortor Driver IN1
#define PIN_LED_IN2   14   // Mortor Driver IN2

#define PIN_SOIL      34

#define PIN_MOTOR_ENB 25
#define PIN_MOTOR_IN3 33
#define PIN_MOTOR_IN4 32
#endif // USE_ARDUINO
/* ARCH and PIN SETTINGS END */

#define SERIAL_BAUD_RATE 9600

/* Sensors, Motors BEGIN */
#define USE_DHT       0

#define USE_CDS       1

#define USE_LED       1
#define USE_CDS_FOR_LED 0

#define USE_SOIL      1
#define SOIL_IS_DRY 3000

#define USE_MOTOR     1
#define USE_SOIL_FOR_MOTOR 0
#define MOTOR_DEFAULT_RUN_TIME_IN_MINUTE  10
#define MOTOR_DEFAULT_WAIT_TIME_IN_MINUTE 1

#define USE_NTP       1
/* Sensors, Motors END */

#define DEFAULT_UPDATE_INTERVAL 1000
#define LEDBUILTIN_UPDATE_INTERVAL 500

#include <DHT.h>

#include <LiquidCrystal_I2C.h>
//#include "lcd-test.ino"

#define USE_LCD       1
#define DO_LCD_TEST   1
#define LCD_ADDR      0x27

#define DEFAULT_LCDLINE1 "Hello World!"
#define DEFAULT_LCDLINE2 "Welcome SmartFarm!"

#define USE_BLYNK       1

#if USE_BLYNK
#define BLYNK_PRINT Serial

#include "blynk-env.h"
/* // in blynk-env.ino
#define BLYNK_TEMPLATE_ID ""
#define BLYNK_TEMPLATE_NAME ""
#define BLYNK_AUTH_TOKEN ""

char ssid[] = "";
char pass[] = "";
*/

#include <WiFi.h>
#include <WiFiClient.h>
#include <BlynkSimpleEsp32.h>

BlynkTimer timer;
#endif // USE_BLYNK

struct status{
  // uptime
  unsigned long now = 0;

  // SERIAL
  unsigned long serial_last_update = 0;
  int serial_update_interval = DEFAULT_UPDATE_INTERVAL;

  // ARDUINO?
  int use_arduino = USE_ARDUINO;

  // DHT
  int use_dht = USE_DHT;
  unsigned long dht_last_check = 0;
  int dht_update_interval = DEFAULT_UPDATE_INTERVAL;

  float humidity = 0.0;
  float temperature = 0.0;
  float heatIndex = 0.0;

  String humiStr;
  String tempStr;
  String hicStr;

  // CDS
  int use_cds = USE_CDS;
  unsigned long cds_last_check = 0;
  int cds_update_interval = DEFAULT_UPDATE_INTERVAL;

  int cdsLight = 0; // light
  String cdsLightStr;

  // LCD
  int use_lcd = USE_LCD;
  unsigned long lcd_last_update = 0;
  int lcd_update_interval = DEFAULT_UPDATE_INTERVAL;

  String lcdLine1 = DEFAULT_LCDLINE1;
  String lcdLine2 = DEFAULT_LCDLINE2;

  // SOIL
  int use_soil = USE_SOIL;
  unsigned long soil_last_check = 0;
  int soil_update_interval = DEFAULT_UPDATE_INTERVAL;

  int soil = 0;
  String soilStr;

  // LED
  int use_led = USE_LED;
  unsigned long led_last_update = 0;
  int led_update_interval = DEFAULT_UPDATE_INTERVAL;
  int led = 0;

  // LEDBUILTIN
  unsigned long ledbuiltin_last_update = 0;
  int ledbuiltin_update_interval = LEDBUILTIN_UPDATE_INTERVAL;
  int ledbuiltin = 0;

  // MOTOR
  int use_motor = USE_MOTOR;
  unsigned long motor_last_update = 0;
  unsigned long motor_duration_running = MOTOR_DEFAULT_RUN_TIME_IN_MINUTE * (unsigned long)(60) * (unsigned long)(1000);
  unsigned long motor_duration_waiting = MOTOR_DEFAULT_RUN_TIME_IN_MINUTE * (unsigned long)(60) * (unsigned long)(1000);
  int motor = 0;
  String motorStr;

  // BLYNK
  int use_blynkd = USE_BLYNK;
  int use_ntp = USE_NTP;
  int running_mode = 0; // 0: manual, 1: automatic
  int led_scheduled_start_time = 9; // LED start time, 9 AM by default
  int led_scheduled_end_time = 21;  // LED end time, 9 PM by default
  int motor_scheduled_break_time = 10;  // break time motor in minutes
  int motor_scheduled_running_time = 1; // duration of running motor in minutes
} g_status;


void process_serial() {
  if (g_status.now - g_status.serial_last_update < g_status.serial_update_interval) {
    return;
  } else {
    g_status.serial_last_update = g_status.now;
  }

  int checking = 0;

  #if USE_DHT
  checking++;
  Serial.print(g_status.tempStr);
  Serial.print(", ");
  Serial.print(g_status.humiStr);
  Serial.print(", ");
  Serial.print(g_status.hicStr);
  Serial.print(", ");
  #endif

  #if USE_CDS
  checking++;
  Serial.print(g_status.cdsLightStr);
  Serial.print(", ");
  #endif

  #if USE_SOIL
  checking++;
  Serial.print(g_status.soilStr);
  Serial.print(", ");
  #endif

  #if USE_MOTOR
  checking++;
  Serial.print(g_status.motorStr);
  Serial.print(", ");
  #endif

  if (checking == 0) {
    Serial.println("Tick!");
  } else {
    Serial.println("");
  }
}

void handle_millis_rollover() {
  
  // millis() rollover in 49.7 days
  Serial.println("WARNING: millis() ROLLOVER DETECTED, RESETTING UPDATE/CHECK TIME");
  g_status.serial_last_update = g_status.now;

  g_status.dht_last_check = g_status.now;
  g_status.cds_last_check = g_status.now;
  g_status.lcd_last_update = g_status.now;
  g_status.soil_last_check = g_status.now;

  g_status.led_last_update = g_status.now;
  if (g_status.use_led == 1) {
    if (g_status.led > 0) {
      led_turn_off();
    }
  }
  
  g_status.ledbuiltin_last_update = g_status.now;
  if (g_status.ledbuiltin > 0) {
    ledbuiltin_turn_off();
  }

  g_status.motor_last_update = g_status.now;
  if (g_status.use_motor == 1) {
    if (g_status.motor > 0) {
      motor_turn_off();
    }
  }
  Serial.println("WARNING: millis() ROLLOVER DETECTED, RESETTING COMPLETED");
}

void process_now() {
  g_status.now = millis();

  if (g_status.now - g_status.ledbuiltin_last_update > g_status.ledbuiltin_update_interval) {
    ledbuiltin_toggle();
  } else if (g_status.now < g_status.ledbuiltin_last_update) {
    handle_millis_rollover();
  }
}

/* NTP BLOCK BEGIN */
#if USE_NTP
void SetTimeConfig()
{
  char* ntpServer = "kr.pool.ntp.org";
  long  gmtOffset_sec = 9 * 3600; // GMT+9 = 9 * 3600
  configTime(gmtOffset_sec, 0, ntpServer); 
}
int getCurrentHour()
{
  time_t now;
  struct tm timeinfo;
  now = time(nullptr);
  localtime_r(&now, &timeinfo);
  
  int nHour = (int)timeinfo.tm_hour;
  //Serial.printf("Current time: %02d:%02d:%02d\n", timeinfo.tm_hour, timeinfo.tm_min, timeinfo.tm_sec);

  return nHour;
}
#endif // USE_NTP
/* NTP BLOCK END */



/* LEDBUILTIN BLOCK BEGIN */
void setup_ledbuiltin() {
  pinMode(LED_BUILTIN, OUTPUT);
  g_status.ledbuiltin = 0;
}

void ledbuiltin_turn_on() {
  if (g_status.ledbuiltin == 0) {
    digitalWrite(LED_BUILTIN, HIGH);
    g_status.ledbuiltin = 1;
    g_status.ledbuiltin_last_update = g_status.now;
  } else {
    Serial.println("LEDBUILTIN ALREADY TURN ON");
  }
}

void ledbuiltin_turn_off() {
  if (g_status.ledbuiltin == 1) {
    digitalWrite(LED_BUILTIN, LOW);
    g_status.ledbuiltin = 0;
    g_status.ledbuiltin_last_update = g_status.now;
  } else {
    Serial.println("LEDBUILTIN ALREADY TURN OFF");
  }
}

void ledbuiltin_toggle() {
  if (g_status.ledbuiltin == 1) {
    ledbuiltin_turn_off();
  } else {
    ledbuiltin_turn_on();
  }
}
/* LEDBUILTIN BLOCK BEGIN */

/* LED BLOCK BEGIN */
void setup_led() {
  #if USE_LED
  pinMode(PIN_LED_ENA, OUTPUT);
  pinMode(PIN_LED_IN1, OUTPUT);
  pinMode(PIN_LED_IN2, OUTPUT);
  #endif
}

void led_set(int led, int in1, int in2) {
  #if USE_LED
  analogWrite(PIN_LED_ENA, led);      // led strength 0 off
  digitalWrite(PIN_LED_IN1, in1);   // LOW -> LOW, no action
  digitalWrite(PIN_LED_IN2, in2);

  g_status.led = led;
  g_status.led_last_update = g_status.now;

  #if USE_BLYNK
  Blynk.virtualWrite(V1, g_status.led > 0);
  #endif // USE_BLYNK

  #endif
}

void led_turn_on() {
  #if USE_LED
  if (g_status.led == 0) {
    led_set(255, HIGH, LOW);
  } else {
    Serial.println("[WARNING] LED is already ON");
  }
  #endif
}

void led_turn_off() {
  #if USE_LED
  if (g_status.led > 0) {
    led_set(0, LOW, LOW);
  } else {
    Serial.println("[WARNING] LED is already OFF");
  }
  #endif
}

void led_toggle() {
  int onoff = 0; // 1 scheduled-on, 2 scheduled-off, 3 on, 4 off

  #if USE_NTP
  int hour = getCurrentHour();

  if (g_status.led_scheduled_start_time < g_status.led_scheduled_end_time) {
    if (g_status.led_scheduled_start_time <= hour && hour < g_status.led_scheduled_end_time) {
      if (g_status.led == 0) {
        onoff = 1;
      }
    } else {
      if (g_status.led > 0) {
        onoff = 2;
      }
    }
  } else if (g_status.led_scheduled_start_time > g_status.led_scheduled_end_time) {
    if (g_status.led_scheduled_start_time <= hour || hour < g_status.led_scheduled_end_time) {
      if (g_status.led == 0) {
        onoff = 1;
      }
    } else {
      if (g_status.led > 0) {
        onoff = 2;
      }
    }
  }
  #endif // USE_NTP

  if (onoff == 0) {
    if (g_status.now - g_status.led_last_update < g_status.led_update_interval) {
      return;
    } else {
      if (g_status.led == 0) {
        onoff = 3;
      } else {
        onoff = 4;
      }
    }
  }
  switch (onoff) {
    case 1:
      Serial.println("[SCHEDULED] LED turn ON");
      led_turn_on();
      break;
    case 2:
      Serial.println("[SCHEDULED] LED turn OFF");
      led_turn_off();
      break;
    case 3:
      Serial.println("[TOGGLE] LED turn ON");
      led_turn_on();
      break;
    case 4:
      Serial.println("[TOGGLE] LED turn OFF");
      led_turn_off();
      break;
  }
}
/* LED BLOCK BEGIN */

/* LCD BLOCK BEGIN */

#if USE_LCD
LiquidCrystal_I2C mylcd(LCD_ADDR, 16, 2);
#endif

void setup_lcd() {
  #if USE_LCD
  
  #if DO_LCD_TEST
  lcd_test_setup();   // Test LCD port number, which is 0x27
  lcd_test();
  #endif

  mylcd.init();       // Initialize LCD
  mylcd.backlight();  // Turn on the backlight of LCD
  mylcd.clear();      // Clear the LCD

  mylcd.setCursor(1, 0);
  mylcd.print(g_status.lcdLine1);
  mylcd.setCursor(1, 1);
  mylcd.print(g_status.lcdLine2);
  #endif
}

void process_lcd() {
  #if USE_LCD
  
  if (g_status.now - g_status.lcd_last_update < g_status.lcd_update_interval) {
    return;
  } else {
    g_status.lcd_last_update = g_status.now;
  }

  mylcd.clear();
  mylcd.setCursor(1, 0);
  mylcd.print(g_status.lcdLine1);
  mylcd.setCursor(1, 1);
  mylcd.print(g_status.lcdLine2);
  #endif
}
/* LCD BLOCK END */

/* DHT BLOCK BEGIN */
#if USE_DHT
DHT dht(PIN_DHT, DHT_TYPE);
#endif

void setup_dht() {
  #if USE_DHT
  dht.begin();        // Initialize DHT sensor

  g_status.temperature = 0;
  g_status.humidity = 0;
  g_status.heatIndex = 0;
  #endif
}

void process_dht() {
  if (g_status.now - g_status.dht_last_check < g_status.dht_update_interval) {
    return;
  } else {
    g_status.dht_last_check = g_status.now;
  }

  #if USE_DHT
  // DHT
  float h = dht.readHumidity();
  float t = dht.readTemperature();

  if (isnan(h) || isnan(t)) {
    Serial.println(F("Failed to read from DHT sensor!"));
    return;
  }

  float hic = dht.computeHeatIndex(t, h, false);

  String tempStr = "Temp: " + String(t) + "'C";
  String humiStr = "Humi: " + String(h) + " %";
  String hicStr = "HI: " + String(hic) + " Â°C";

  g_status.temperature = t;
  g_status.humidity = h;
  g_status.heatIndex = hic;
  g_status.tempStr = tempStr;
  g_status.humiStr = humiStr;
  g_status.hicStr = hicStr;

  String lcdStr = String(t) + " " + String(h) + " " + String(hic);
  g_status.lcdLine1 = lcdStr;
  #endif
}
/* DHT BLOCK END */

/* CDS BLOCK BEGIN */
void setup_cds() {
  #if USE_CDS
  pinMode(PIN_CDS, INPUT);  // Initialize CDS

  g_status.cdsLight = 0;
  #endif
}

void process_cds() {
  #if USE_CDS
  if (g_status.now - g_status.cds_last_check < g_status.cds_update_interval) {
    return;
  } else {
    g_status.cds_last_check = g_status.now;
  }

  // CDS Photoresistor
  int cdsLight = digitalRead(PIN_CDS);
  String cdsLightStr = "Light: " + String(cdsLight) + (cdsLight == 1? " OFF" : " ON");

  g_status.cdsLight = cdsLight;
  g_status.cdsLightStr = cdsLightStr;

  #if USE_LED
  #if USE_CDS_FOR_LED
  if (cdsLight == HIGH) { // Light: 1 OFF
    led_turn_on();
  } else {
    led_turn_off();
  }
  #endif // USE_CDS_FOR_LED
  #endif  // USE_LED

  #endif  // USE_CDS
}
/* CDS BLOCK END */

/* SOIL BLOCK BEGIN */
void setup_soil() {
  #if USE_SOIL
  pinMode(PIN_SOIL, INPUT);

  g_status.soil = 0;
  #endif
}

void process_soil() {
  #if USE_SOIL
  if (g_status.now - g_status.soil_last_check < g_status.soil_update_interval) {
    return;
  } else {
    g_status.soil_last_check = g_status.now;
  }

  int soil = analogRead(PIN_SOIL);
  float soilPerCent = (1024.0 - (double)soil) / 1024.0 * 100.0;

  String soilStr = "S: " + String(soil) + ", " + String(soilPerCent);

  g_status.soil = soil;
  g_status.soilStr = soilStr;
  g_status.lcdLine2 = soilStr;

  #if USE_SOIL_FOR_MOTOR
  if (g_status.soil > SOIL_IS_DRY) {
    motor_turn_on();
  } else {
    motor_turn_off();
  }
  #endif
  #endif
}
/* SOIL BLOCK END */

/* MOTOR BLOCK BEGIN */
void setup_motor() {
  #if USE_MOTOR
  pinMode(PIN_MOTOR_ENB, OUTPUT);
  pinMode(PIN_MOTOR_IN3, OUTPUT);
  pinMode(PIN_MOTOR_IN4, OUTPUT);
  #endif
}

void motor_set(int motor, int in3, int in4) {
  #if USE_MOTOR
  analogWrite(PIN_MOTOR_ENB, motor);
  digitalWrite(PIN_MOTOR_IN3, in3);
  digitalWrite(PIN_MOTOR_IN4, in4);

  g_status.motor = motor;
  g_status.motor_last_update = g_status.now;
  g_status.motorStr = "M: " + String(motor) + " " + (in3 == HIGH ? "HIGH" : "LOW") + " " + (in4 == HIGH ? "HIGH" : "LOW");

  #if USE_BLYNK
  Blynk.virtualWrite(V2, g_status.motor > 0);
  #endif // USE_BLYNK
  #endif
}

void motor_turn_on() {
  #if USE_MOTOR
  if (g_status.motor == 0) {
    motor_set(255, HIGH, LOW);
  } else {
    Serial.println("MOTOR is ALREADY ON");
  }
  #endif
}

void motor_turn_off() {
  #if USE_MOTOR
  if (g_status.motor > 0) {
    motor_set(0, LOW, LOW);
  } else {
    Serial.println("MOTOR is ALREADY OFF");
  }
  #endif
}

void motor_toggle() {
  int onoff = 0; // 1 scheduled-on, 2 scheduled-off, 3 on, 4 off

  if (g_status.motor > 0) { // MOTOR is ON
    if (g_status.now - g_status.motor_last_update >= g_status.motor_duration_running) {
      Serial.println("[TOGGLE] MOTOR turn OFF");
      motor_turn_off();
    }
  } else {
    if (g_status.now - g_status.motor_last_update >= g_status.motor_duration_waiting) {
      Serial.println("[TOGGLE] MOTOR turn ON");
      motor_turn_on();
    }
  }
}
/* MOTOR BLOCK END */

/* BLYNK BLOCK BEGIN */
#if USE_BLYNK
// Running Mode 
BLYNK_WRITE(V0)
{
  int pinValue = param.asInt(); 
  //Serial.println(pinValue);

  g_status.running_mode = pinValue;
}

BLYNK_WRITE(V1)
{
  int pinValue = param.asInt(); // assigning incoming value from pin V1 to a variable
  
  if (g_status.running_mode == 0) {
    if (pinValue == 1) {
      if (g_status.led == 0) {
        led_turn_on();
      }
    } else {
      if (g_status.led > 0) {
        led_turn_off();
      }
    }
  }
}

BLYNK_WRITE(V2)
{
  int pinValue = param.asInt(); // assigning incoming value from pin V1 to a variable
  
  if (g_status.running_mode == 0) {
    if (pinValue == 1) {
      if (g_status.motor == 0) {
        motor_turn_on();
      }
    } else {
      if (g_status.motor > 0) {
        motor_turn_off();
      }
    }
  }
}

BLYNK_CONNECTED() { // setup() for BLYNK
  Blynk.syncVirtual(V0); // Automatic / Manual mode
  Blynk.syncVirtual(V1); // LED on/off
  Blynk.syncVirtual(V2); // Motor on/off
}

#endif // USE_BLYNK

void blynk_update() {
  #if USE_BLYNK
  // V1 LED
  // V2 PUMP
  // V3 Temperature
  // V4 Humidity
  #if USE_DHT
  Blynk.virtualWrite(V3, (int)g_status.temperature);
  Blynk.virtualWrite(V4, (int)g_status.humidity);
  #endif // USE_DHT

  #if USE_LED
  Blynk.virtualWrite(V1, g_status.led > 0);
  #endif // USE_LED

  #if USE_MOTOR
  Blynk.virtualWrite(V2, g_status.motor > 0);
  #endif // USE_MOTOR

  if (g_status.running_mode == 1) { // AUTO mode
    led_toggle();
    motor_toggle();
  }
  #endif
}

void setup_blynk() {
  #if USE_BLYNK
  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, pass);
  timer.setInterval(1000L, blynk_update);
  #endif
}

void process_blynk() {
  #if USE_BLYNK
  Blynk.run();
  timer.run();
  #endif
}
/* BLYNK BLOCK END */


void setup() {
  Serial.begin(SERIAL_BAUD_RATE);
  Serial.println(F("\nLet's build a smart farm!"));

  process_now();

  setup_led();
  setup_ledbuiltin();
  setup_motor();

  setup_dht();
  setup_lcd();
  setup_cds();
  setup_soil();

  setup_blynk();
}


void loop() {
  process_now();

  process_dht();
  process_cds();
  process_soil();

  process_lcd();
  process_serial();

  process_blynk();
}